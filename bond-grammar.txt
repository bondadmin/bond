External Definitions
--------------------

translation_unit
  : external_declaration*

external_declaration
  : namespace_definition
  | function_definition
  | function_declaration
  | struct_declaration
  | enum_declaration
  | const_declaration

namespace_definition
  : NAMESPACE IDENTIFIER '{' external_declaration* '}'

function_definition
  : function_prototype compound_statement


Declarations
------------

function_declaration
  : function_prototype ';'

function_prototype
  : declarator IDENTIFIER '(' [declarator_list] ')'

struct_declaration
  : STRUCT IDENTIFIER '{' struct_member_declaration+ '}' ';'

struct_member_declaration
  : field_declaration
  | function_declaration

field_declaration
  : declarator ';'

enum_declaration
  : ENUM IDENTIFIER '{' enumerator_list [',] '}' ';'

enumerator_list
  : enumerator
  | enumerator_list ',' enumerator

enumerator
  : IDENTIFIER ['=' const_expression]

declaration
  : const_declaration
  | variable_declaration

const_declaration
  : const_type_descriptor namespaced_identifier '=' const_initializer ';'

variable_declaration
  : declarator ['=' initializer] ';'

declarator
  : type_descriptor namespaced_identifier

declarator_list
  : declarator
  | declarator_list ',' declarator

const_type_descriptor
  : CONST primitive_type_specifier
  | primitive_type_specifier CONST
  | type_descriptor '*' CONST
  | type_descriptor '[' [const_expression] ']'

type_descriptor
  : CONST type_specifier
  | type_specifier [CONST]
  | type_descriptor '*' [CONST]
  | type_descriptor '[' [const_expression] ']'

type_specifier
  : primitive_type_specifier
  | namespaced_identifier

primitive_type_specifier
  : VOID
  | CHAR
  | INT
  | UINT
  | FLOAT

const_initializer_list
  : const_initializer
  | const_initializer_list ',' const_initializer

const_initializer
  : const_expression
  | '{' const_initializer_list '}'
  | '{' const_initializer_list ',' '}'

initializer_list
  : initializer
  | initializer_list ',' initializer

initializer
  : assignment_expression
  | '{' initializer_list [','] '}'

namespaced_identifier
  : IDENTIFIER
  | namespaced_identifier '::' IDENTIFIER


Statements
----------

statement
  : labeled_statement
  | compound_statement
  | expression_statement
  | selection_statement
  | iteration_statement
  | jump_statement
  | declaration

labeled_statement
  : IDENTIFIER ':' statement
  | CASE const_expression ':' statement
  | DEFAULT ':' statement

compound_statement
  : '{' declaration* statement* '}'

expression_statement
  : [expression] ';'

selection_statement
  : IF '(' expression ')' statement [ELSE statement]
  | SWITCH '(' expression ')' statement

iteration_statement
  : WHILE '(' expression ')' statement
  | DO statement WHILE '(' expression ')' ';'
  | FOR '(' [expression] ';' [expression] ';' [expression] ')' statement

jump_statement
  : CONTINUE ';'
  | BREAK ';'
  | RETURN [expression] ';'


Expressions
-----------

primary_expression
  : namespaced_identifier
  | CONSTANT
  | STRING_LITERAL
  | '(' expression ')'

postfix_expression
  : primary_expression
  | postfix_expression '[' expression ']'
  | postfix_expression '(' ')'
  | postfix_expression '(' argument_expression_list ')'
  | postfix_expression '.' IDENTIFIER
  | postfix_expression '->' IDENTIFIER
  | postfix_expression '++'
  | postfix_expression '--'

argument_expression_list
  : assignment_expression
  | argument_expression_list ',' assignment_expression

unary_expression
  : postfix_expression
  | '++' unary_expression
  | '--' unary_expression
  | '&' cast_expression
  | '*' cast_expression
  | '+' cast_expression
  | '-' cast_expression
  | '~' cast_expression
  | '!' cast_expression
  | SIZEOF unary_expression
  | SIZEOF '(' type_name ')'

cast_expression
  : unary_expression
  | '(' type_name ')' cast_expression

multiplicative_expression
  : cast_expression
  | multiplicative_expression '*' cast_expression
  | multiplicative_expression '/' cast_expression
  | multiplicative_expression '%' cast_expression

additive_expression
  : multiplicative_expression
  | additive_expression '+' multiplicative_expression
  | additive_expression '-' multiplicative_expression

shift_expression
  : additive_expression
  | shift_expression '<<' additive_expression
  | shift_expression '>>' additive_expression

relational_expression
  : shift_expression
  | relational_expression '<' shift_expression
  | relational_expression '>' shift_expression
  | relational_expression '<=' shift_expression
  | relational_expression '>=' shift_expression

equality_expression
  : relational_expression
  | equality_expression '==' relational_expression
  | equality_expression '!=' relational_expression

and_expression
  : equality_expression
  | and_expression '&' equality_expression

exclusive_or_expression
  : and_expression
  | exclusive_or_expression '^' and_expression

inclusive_or_expression
  : exclusive_or_expression
  | inclusive_or_expression '|' exclusive_or_expression

logical_and_expression
  : inclusive_or_expression
  | logical_and_expression '&&' inclusive_or_expression

logical_or_expression
  : logical_and_expression
  | logical_or_expression '||' logical_and_expression

conditional_expression
  : logical_or_expression
  | logical_or_expression '?' expression ':' conditional_expression

assignment_expression
  : conditional_expression
  | unary_expression '=' assignment_expression
  | unary_expression '<<=' assignment_expression
  | unary_expression '>>=' assignment_expression
  | unary_expression '+=' assignment_expression
  | unary_expression '-=' assignment_expression
  | unary_expression '*=' assignment_expression
  | unary_expression '/=' assignment_expression
  | unary_expression '%=' assignment_expression
  | unary_expression '&=' assignment_expression
  | unary_expression '^=' assignment_expression
  | unary_expression '|=' assignment_expression

expression
  : assignment_expression
  | expression ',' assignment_expression
